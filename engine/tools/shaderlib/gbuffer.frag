#version 450

// G-buffer geometry pass (fragment), MRT output:
//   color[0] = packed view depth (identical to depth.frag, so SSAO/SSR depth is unchanged)
//   color[1] = view-space normal (octahedral .rg) + roughness (.b) + metallic (.a)
//   color[2] = linear base color (.rgb) + IBL source (.a):
//              0 = none, 0.5 = sky, 1 = local reflection probe

layout(location = 0) out vec4 frag_depth;
layout(location = 1) out vec4 frag_gbuffer;
layout(location = 2) out vec4 frag_albedo;

in vec2 v_projZW;
in vec3 v_normal;

uniform u_fs_gbufferMaterial {
    vec4 params;        // x = roughness, y = metallic, z = IBL source, w = alpha-cutout
    vec4 baseColorFactor;
} gbufferMaterial;

#if defined(HAS_BASECOLOR_TEXTURE) || defined(HAS_METALLICROUGHNESS_TEXTURE)
    in vec2 v_uv1;
#endif
#if defined(HAS_BASECOLOR_TEXTURE)
    uniform texture2D u_baseColorTexture;
    uniform sampler u_baseColor_smp;
#endif
#if defined(HAS_METALLICROUGHNESS_TEXTURE)
    uniform texture2D u_metallicRoughnessTexture;
    uniform sampler u_metallicRoughness_smp;
#endif

#include "includes/depth_util.glsl"
#include "includes/octahedral.glsl"
#include "includes/srgb.glsl"

void main() {
    vec4 baseColor = gbufferMaterial.baseColorFactor;
    #if defined(HAS_BASECOLOR_TEXTURE)
        vec4 texColor = texture(sampler2D(u_baseColorTexture, u_baseColor_smp), v_uv1);
        // alpha cutout (only for cutout materials, matching the depth/shadow pass)
        if (gbufferMaterial.params.w > 0.5 && texColor.a < 0.5) {
            discard;
        }
        baseColor *= sRGBToLinear(texColor);
    #endif

    float roughness = gbufferMaterial.params.x;
    float metallic  = gbufferMaterial.params.y;
    #if defined(HAS_METALLICROUGHNESS_TEXTURE)
        // glTF convention: green = roughness, blue = metallic
        vec4 mr = texture(sampler2D(u_metallicRoughnessTexture, u_metallicRoughness_smp), v_uv1);
        roughness *= mr.g;
        metallic  *= mr.b;
    #endif

    // Higher precision equivalent of gl_FragCoord.z (see depth.frag).
    frag_depth = encodeDepth(0.5 * v_projZW[0] / v_projZW[1] + 0.5);

    vec3 n = normalize(v_normal);
    frag_gbuffer = vec4(octEncode(n), clamp(roughness, 0.0, 1.0), clamp(metallic, 0.0, 1.0));

    frag_albedo = vec4(baseColor.rgb, gbufferMaterial.params.z);
}
